use super::ast::{Array, Field, Function, FunctionType, FunctionPointer, Identifier, Import, Item, ItemType, Location, Owned, Primitive, Pointer, PointerModifier, Slice, StringPointer, Struct, Type};
use super::lexer;

grammar(file_id: usize);

extern {
    type Location = usize;
    type Error = ();

    enum lexer::Token {
        "{" => lexer::Token::LeftBracket,
        "}" => lexer::Token::RightBracket,
        "(" => lexer::Token::LeftParen,
        ")" => lexer::Token::RightParen,
        "[" => lexer::Token::LeftSquareBracket,
        "]" => lexer::Token::RightSquareBracket,
        ":" => lexer::Token::Colon,
        "," => lexer::Token::Comma,
        ";" => lexer::Token::Semicolon,
        "*" => lexer::Token::Asterisk,
        "->" => lexer::Token::Arrow,
        "primitive" => lexer::Token::Primitive(<Primitive>),
        "struct" => lexer::Token::Struct,
        "const" => lexer::Token::Const,
        "owned" => lexer::Token::Owned,
        "mut" => lexer::Token::Mut,
        "string" => lexer::Token::String,
        "import" => lexer::Token::Import,
        "function" => lexer::Token::Function,
        "closure" => lexer::Token::Closure,
        "string-literal" => lexer::Token::StringLiteral(<String>),
        "integer-literal" => lexer::Token::IntegerLiteral(<u64>),
        "identifier" => lexer::Token::Identifier(<String>),
        "comment" => lexer::Token::Comment(<String>),
        "doc-comment" => lexer::Token::DocComment(<String>),
    }
}

String: String =
    "string-literal" => <>;

Integer: u64 =
    "integer-literal" => <>;

Comment: String =
    "comment" => <>.trim().trim_start_matches("//").to_string();

DocComment: String =
    "doc-comment" => <>.trim().trim_start_matches("///").to_string();

Primitive: Primitive =
    "primitive" => <>;

IdentifierString: String =
    "identifier" => <>;

Identifier: Identifier =
    <l: @L> <identifier: IdentifierString> <r: @R> => Identifier { location: Location::new(file_id, l..r), identifier };

PointerModifier: PointerModifier = {
    "const" => PointerModifier::Const,
    "mut" => PointerModifier::Mut,
}

Pointer: Pointer =
    <l: @L> <modifier: PointerModifier> "*" <ty: Type> <r: @R> => Pointer { location: Location::new(file_id, l..r), modifier, ty: Box::new(ty) };

StringPointer: StringPointer =
    <l: @L> <modifier: PointerModifier> "string" <r: @R> => StringPointer { location: Location::new(file_id, l..r), modifier };

Slice: Slice =
    <l: @L> <modifier: PointerModifier> "[" <ty: Type> "]" <r: @R> => Slice { location: Location::new(file_id, l..r), modifier, ty: Box::new(ty) };

OwnedTy: Type = {
    <pointer: Pointer> => Type::Pointer(pointer),
    <string: StringPointer> => Type::String(string),
    <slice: Slice> => Type::Slice(slice),
}

Owned: Owned =
    <l: @L> "owned" <ty: Type> <r: @R> => Owned { location: Location::new(file_id, l..r), ty: Box::new(ty) };

FunctionType: FunctionType = {
    "function" => FunctionType::Function,
    "closure" => FunctionType::Closure,
}

FunctionArg: (Identifier, Type) =
    <ident: Identifier> ":" <ty: Type> => (ident, ty);

FunctionPointer: FunctionPointer =
    <l: @L> <fn_ty: FunctionType> "(" <args: Comma<FunctionArg>> ")" "->" <returns: Type> <r: @R> => FunctionPointer {
        location: Location::new(file_id, l..r),
        fn_ty,
        args,
        returns: Box::new(returns)
    };

Array: Array =
    <l: @L> "[" <ty: Type> ";" <len: Integer> "]" <r: @R> => Array { location: Location::new(file_id, l..r), ty: Box::new(ty), len };

Type: Type = {
    <l: @L> <primitive: Primitive> <r: @R> => Type::Primitive { location: Location::new(file_id, l..r), primitive },
    <pointer: Pointer> => Type::Pointer(pointer),
    <string: StringPointer> => Type::String(string),
    <slice: Slice> => Type::Slice(slice),
    <owned: Owned> => Type::Owned(owned),
    <identifier: Identifier> => Type::Identifier(identifier),
    <fn_pointer: FunctionPointer> => Type::FunctionPointer(fn_pointer),
    <array: Array> => Type::Array(array),
}

Comma<T>: Vec<T> = {
    <mut v:(<T> ",")*> <e:T?> => match e {
        None => v,
        Some(e) => {
            v.push(e);
            v
        }
    }
};

Field: Field =
    <docs: DocComment*> <name: Identifier> ":" <ty: Type> => Field { docs, name, ty };

StructContents: Option<Vec<Field>> = {
    "{" <fields: Comma<Field>> "}" => Some(fields),
    ";" => None,
}

Struct: Struct =
    <l: @L> "struct" <name: Identifier> <fields: StructContents> <r: @R> => Struct { location: Location::new(file_id, l..r), name, fields };

Import: Import =
    <l: @L> "import" <path: String> <r: @R> => Import { location: Location::new(file_id, l..r), path, resolved: None };

Function: Function =
    <l: @L> "function" <name: Identifier> "(" <args: Comma<FunctionArg>> ")" "->" <returns: Type> ";" <r: @R> => Function {
        location: Location::new(file_id, l..r),
        name,
        args,
        returns: Box::new(returns),
    };

ItemType: ItemType = {
    <s: Struct> => ItemType::Struct(s),
    <i: Import> => ItemType::Import(i),
    <f: Function> => ItemType::Function(f),
}

Item: Item =
    <docs: DocComment*> <item: ItemType> => Item { docs, item };

pub Program: Vec<Item> =
    <items: Item*> => <>;
